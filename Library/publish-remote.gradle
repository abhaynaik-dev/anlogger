apply plugin: 'maven-publish'
apply plugin: 'signing'


task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll setup later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
//                artifacts javaDocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = "A small android library to write Timber logs into a file"
                    url = 'https://github.com/abhaynaik-dev/anlogger/tree/main'
                    licenses {
                        license {
                            name = 'MIT license'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'abhaynaik-dev'
                            name = 'Abhay Naik'
                            email = 'abhaynaik9008@gmail.com'
                        }
                    }

                    // Version control info
                    scm {
                        connection = 'scm:git:github.com/abhaynaik-dev/anlogger.git'
                        developerConnection = 'scm:git:ssh://github.com/abhaynaik-dev/anlogger.git'
                        url = 'https://bitbucket.org/username/mavenpublishlibrary/src/master/'
                    }
                }
            }
        }
    }
}


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
signing {
    useInMemoryPgpKeys(
            properties.getProperty('signing.keyId'),
            properties.getProperty('signing.key'),
            properties.getProperty('signing.password')
    )
    sign publishing.publications
}